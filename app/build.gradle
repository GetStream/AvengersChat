plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'kotlin-parcelize'
    id 'dagger.hilt.android.plugin'
    id 'androidx.navigation.safeargs.kotlin'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
}

apply from: "$rootDir/dependencies.gradle"

android {
    compileSdk versions.compileSdk
    defaultConfig {
        applicationId "io.getstream.avengerschat"
        minSdk versions.minSdk
        targetSdk versions.compileSdk
        versionCode versions.versionCode
        versionName versions.versionName
        vectorDrawables.useSupportLibrary = true

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
        javaCompileOptions {
            annotationProcessorOptions {
                arguments["dagger.hilt.disableModulesHaveInstallInCheck"] = "true"
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = "11"
    }

    buildFeatures {
        dataBinding true
    }

    lintOptions {
        abortOnError false
    }

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
        kotlinOptions.freeCompilerArgs += ["-Xopt-in=kotlinx.coroutines.ExperimentalCoroutinesApi"]
    }
}

dependencies {
    // android supports
    implementation "com.google.android.material:material:$versions.materialVersion"

    // jetpack components
    implementation "androidx.fragment:fragment-ktx:$versions.fragmentVersion"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$versions.lifecycleVersion"
    implementation "androidx.navigation:navigation-fragment-ktx:$versions.navigationVersion"
    implementation "androidx.navigation:navigation-ui-ktx:$versions.navigationVersion"
    implementation "androidx.navigation:navigation-dynamic-features-fragment:$versions.navigationVersion"
    implementation "androidx.room:room-ktx:$versions.roomVersion"
    kapt "androidx.room:room-compiler:$versions.roomVersion"

    // stream chat core + UI SDK
    implementation "io.getstream:stream-chat-android-ui-components:$versions.streamChatUIVersion"
    implementation "io.getstream:stream-chat-android-pushprovider-firebase:$versions.streamChatUIVersion"

    // binding
    implementation("com.github.skydoves:bindables:$versions.bindablesVersion") {
        exclude group: "com.google.android.material"
    }

    // startup
    implementation "androidx.startup:startup-runtime:$versions.startupVersion"

    // hilt
    implementation "com.google.dagger:hilt-android:$versions.hiltCoreVersion"
    kapt "com.google.dagger:hilt-compiler:$versions.hiltCoreVersion"

    // network
    implementation "com.github.skydoves:sandwich:$versions.sandwichVersion"
    implementation "com.squareup.retrofit2:retrofit:$versions.retrofitVersion"
    implementation "com.squareup.retrofit2:converter-moshi:$versions.retrofitVersion"
    implementation "com.squareup.okhttp3:logging-interceptor:$versions.okhttpVersion"

    // moshi
    implementation "com.squareup.moshi:moshi-kotlin:$versions.moshiVersion"
    kapt "com.squareup.moshi:moshi-kotlin-codegen:$versions.moshiVersion"

    // coroutines
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$versions.coroutinesVersion"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$versions.coroutinesVersion"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$versions.coroutinesVersion"

    // image loading
    implementation "io.coil-kt:coil:$versions.coilVersion"
    implementation "io.coil-kt:coil-gif:$versions.coilVersion"

    // youtube player
    implementation "com.pierfrancescosoffritti.androidyoutubeplayer:core:$versions.youtubePlayerVersion"

    // bundler
    implementation "com.github.skydoves:bundler:$versions.bundlerVersion"

    // custom views
    implementation "com.github.skydoves:transformationlayout:$versions.transformationLayout"
    implementation "com.github.skydoves:androidveil:$versions.androidVeilVersion"
    implementation "com.yarolegovich:discrete-scrollview:$versions.discreteScrollview"

    implementation platform("com.google.firebase:firebase-bom:$versions.firebaseVersion")
    implementation "com.google.firebase:firebase-crashlytics"
    implementation "com.google.firebase:firebase-messaging"
    implementation "com.google.firebase:firebase-analytics"

    // debugging
    implementation "com.jakewharton.timber:timber:$versions.timberVersion"
}

apply from: "$rootDir/spotless/spotless.gradle"